class_name WorldMain
extends Node


var world_info: Dictionary


func _ready() -> void:
	# Server tick rate
	# For comparaison:
	# Eve Online - 1 tick par second.
	# Fortnite (Battle royale 100 players) - 30 ticks per second.
	# Albion Online - 2 ticks per second (to verify).
	# Valorant (5v5 FPS game) - 128 ticks per second.
	# I believe it depends of your game and architecture, it's a large topic.
	#
	# Raised 10 -> 20 so the simulation matches the entity send rate.
	# StateSynchronizerManagerServer ships entity state at send_rate_hz_entities =
	# 20, but the sim only moved things ten times a second, so half of those send
	# slots found an empty dirty map and sent nothing at all (the early return in
	# _send_entity_deltas_one_shot). Matching the two halves the movement
	# correction window — 100 ms -> 50 ms, which is what rubber-banding keys on —
	# and takes ~50 ms off worst-case hit latency.
	#
	# This is NOT free. Those suppressed slots now carry real deltas, so entity
	# bandwidth for anything moving roughly doubles, and the send path does real
	# work 20x/s instead of 10x/s on top of the doubled simulation. Affordable at
	# the population this was measured at — the live world sat at 20.6% of one core
	# with 6 players, ~20 ms of work inside a 100 ms budget — but it scales with
	# player count, and max_players is 200. Watch it before a big event.
	Engine.set_physics_ticks_per_second(20) # 60 by default
	
	if DisplayServer.get_name() != "headless":
		DisplayServer.window_set_title("World Server")
	else:
		# Headless has no vsync, so _process — which drives the sync manager's
		# 20/10 Hz send accumulators — would spin uncapped and burn a full core.
		# 60 fps keeps the send timing accurate at a fraction of the CPU.
		Engine.max_fps = 60
	
	# Default config path. to use another one, override this;
	# or write --config=config_file_path.cfg as a launch argument.
	world_info = ConfigFileUtils.load_section_with_defaults(
		"world-server",
		CmdlineUtils.get_parsed_args().get("config", "res://data/config/world_config.cfg"),
		{
			"name": "NoName",
			"max_players": 200,
			"hardcore": false,
			"motd": "Welcome!",
			"bonus_xp": 0.0,
			"max_character": 5,
			"pvp": true
		}
		
	)
	await get_tree().create_timer(0.5).timeout
	if world_info.has("error"):
		printerr("World server loading configuration failed.")
	else:
		$Database.start_database(world_info)
		$WorldManagerClient.start_client_to_master_server(world_info)
		$WorldServer.start_world_server()
